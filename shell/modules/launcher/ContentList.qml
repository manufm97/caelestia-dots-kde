pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Controls
import Caelestia
import Caelestia.Config
import qs.components
import qs.components.controls
import qs.services
import qs.utils

Item {
    id: root

    required property var content
    required property DrawerVisibilities visibilities
    required property var panels
    required property real maxHeight
    required property StyledTextField search
    required property int padding
    required property int rounding

    readonly property bool showWallpapers: search.text.startsWith(`${GlobalConfig.launcher.actionPrefix}wallpaper `)
    onShowWallpapersChanged: {
        if (showWallpapers) {
            for (let category of Wallpapers.categories) {
                let walls = Wallpapers.grouped[category] || [];
                if (walls.some(w => w.path === Wallpapers.actualCurrent)) {
                    currentWallpaperTab = category;
                    break;
                }
            }
        }
    }
    readonly property bool showWindowSwitcher: search.text.startsWith(`${GlobalConfig.launcher.actionPrefix}windows `)
    readonly property bool showKeybinds: search.text.startsWith(`${GlobalConfig.launcher.actionPrefix}keybinds `)
    readonly property bool showAnimations: search.text.startsWith(`${GlobalConfig.launcher.actionPrefix}animations `)
    readonly property var currentList: showWallpapers ? wallpaperList.item : (showWindowSwitcher ? windowSwitcherList.item : (showAnimations ? animationsList.item : (showKeybinds ? keybindsList.item : appList.item)))

    property string currentWallpaperTab: "Main"

    readonly property var wallpaperTabs: {
        const res = [];
        for (let dir of Wallpapers.categories) {
            res.push({ id: dir, text: dir });
        }
        return res;
    }

    anchors.horizontalCenter: parent.horizontalCenter
    anchors.bottom: parent.bottom

    width: implicitWidth
    height: implicitHeight

    clip: true
    state: showAnimations ? "animations" : (showWindowSwitcher ? "windowSwitcher" : (showKeybinds ? "keybinds" : (showWallpapers ? "wallpapers" : "apps")))

    Behavior on state {
        SequentialAnimation {
            Anim {
                target: root
                property: "opacity"
                from: 1
                to: 0
                type: Anim.DefaultEffects
            }
            PropertyAction {}
            Anim {
                target: root
                property: "opacity"
                from: 0
                to: 1
                type: Anim.DefaultEffects
            }
        }
    }

    states: [
        State {
            name: "apps"

            PropertyChanges {
                target: root
                implicitWidth: root.Tokens.sizes.launcher.itemWidth
                implicitHeight: Math.min(root.maxHeight, appList.implicitHeight > 0 ? appList.implicitHeight : empty.implicitHeight)
            }

        },
        State {
            name: "wallpapers"

            PropertyChanges {
                target: root
                implicitWidth: Math.max(root.Tokens.sizes.launcher.itemWidth * 1.2, wallpaperList.implicitWidth)
                implicitHeight: root.Tokens.sizes.launcher.wallpaperHeight + 56 // Extra space for color buttons
            }
        },
        State {
            name: "windowSwitcher"

            PropertyChanges {
                target: root
                implicitWidth: Math.max(root.Tokens.sizes.launcher.itemWidth * 1.2, windowSwitcherList.implicitWidth)
                implicitHeight: root.Tokens.sizes.launcher.windowSwitcherHeight
            }
        },
        State {
            name: "keybinds"

            PropertyChanges {
                target: root
                implicitWidth: root.Tokens.sizes.launcher.itemWidth
                implicitHeight: Math.min(root.maxHeight, root.Tokens.sizes.launcher.itemHeight * 7)
            }

        },
        State {
            name: "animations"

            PropertyChanges {
                target: root
                implicitWidth: root.Tokens.sizes.launcher.itemWidth
                implicitHeight: Math.min(root.maxHeight, root.Tokens.sizes.launcher.itemHeight * 7)
            }

        }
    ]

    Timer {
        id: keybindsTimer

        interval: 50
        onTriggered: {
            if (state === "keybinds" && keybindsList.item) {
                keybindsList.item.refreshModel();
            }
        }
    }

    // Each list owns its own `active`, derived from the state it belongs to.
    // It used to be set from two places at once — `PropertyChanges` in the states
    // and an imperative onStateChanged handler — which broke state restoration:
    // entering a state captured the value the imperative write had just set, so
    // leaving it "restored" active back to true and the old list stayed loaded,
    // drawing on top of the new one. Binding to root.state (rather than the
    // show* flags) keeps the existing cross-fade timing, since the state change
    // itself is deferred by the Behavior below.
    Loader {
        id: appList

        active: root.state === "apps"

        anchors.fill: parent

        sourceComponent: AppList {
            search: root.search
            visibilities: root.visibilities
        }
    }

    Loader {
        id: wallpaperList

        asynchronous: true
        active: root.state === "wallpapers"

        anchors.top: parent.top
        anchors.horizontalCenter: parent.horizontalCenter
        height: root.Tokens.sizes.launcher.wallpaperHeight

        sourceComponent: WallpaperList {
            search: root.search
            visibilities: root.visibilities
            panels: root.panels
            content: root.content
            contentList: root
        }
    }

    Item {
        id: wallpaperTabsWrapper

        anchors.horizontalCenter: parent.horizontalCenter
        anchors.bottom: parent.bottom
        anchors.bottomMargin: Tokens.padding.medium
        implicitWidth: Math.min(parent.width - Tokens.padding.large * 2, tabsRow.implicitWidth)
        implicitHeight: tabsRow.implicitHeight + indicator.implicitHeight + 5

        visible: root.state === "wallpapers"

        Flickable {
            id: tabsFlickable
            anchors.fill: parent
            contentWidth: tabsRow.implicitWidth
            contentHeight: parent.height
            flickableDirection: Flickable.HorizontalFlick
            clip: true
            
            ScrollBar.horizontal: StyledScrollBar {
                flickable: tabsFlickable
                active: tabsFlickable.moving || tabsFlickable.dragging
            }

            Row {
                id: tabsRow
                spacing: Tokens.spacing.large

                Repeater {
                    id: tabsRepeater
                    model: root.wallpaperTabs

                    delegate: Item {
                        id: tab
                        required property var modelData
                        required property int index

                        readonly property bool current: root.currentWallpaperTab === tab.modelData.id

                        implicitWidth: label.implicitWidth + Tokens.padding.medium * 2
                        implicitHeight: label.implicitHeight + Tokens.padding.small * 2

                        CustomMouseArea {
                            anchors.fill: parent
                            hoverEnabled: true
                            cursorShape: Qt.PointingHandCursor

                            function onWheel(event: WheelEvent): void {
                                let idx = root.wallpaperTabs.findIndex(t => t.id === root.currentWallpaperTab);
                                if (event.angleDelta.y < 0 || event.angleDelta.x < 0)
                                    idx = Math.min(idx + 1, root.wallpaperTabs.length - 1);
                                else if (event.angleDelta.y > 0 || event.angleDelta.x > 0)
                                    idx = Math.max(idx - 1, 0);
                                
                                root.currentWallpaperTab = root.wallpaperTabs[idx].id;
                            }

                            StateLayer {
                                anchors.fill: parent
                                radius: Tokens.rounding.medium
                                color: tab.current ? Colours.palette.m3primary : Colours.palette.m3onSurface
                                onClicked: root.currentWallpaperTab = tab.modelData.id
                            }
                        }

                        StyledText {
                            id: label
                            anchors.centerIn: parent
                            text: tab.modelData.text
                            color: tab.current ? Colours.palette.m3primary : Colours.palette.m3onSurfaceVariant
                            font: Tokens.font.label.large
                        }
                    }
                }
            }

            Item {
                id: indicator

                anchors.top: tabsRow.bottom
                anchors.topMargin: 5

                property int currentIndex: Math.max(0, root.wallpaperTabs.findIndex(t => t.id === root.currentWallpaperTab))
                property Item currentTab: tabsRepeater.itemAt(currentIndex)

                implicitWidth: currentTab ? currentTab.implicitWidth : 0
                implicitHeight: 3
                x: currentTab ? tabsRow.x + currentTab.x : 0

                onCurrentIndexChanged: {
                    if (currentTab) {
                        const targetX = currentTab.x;
                        const targetWidth = currentTab.implicitWidth;
                        if (targetX < tabsFlickable.contentX)
                            tabsFlickable.contentX = targetX;
                        else if (targetX + targetWidth > tabsFlickable.contentX + tabsFlickable.width)
                            tabsFlickable.contentX = targetX + targetWidth - tabsFlickable.width;
                    }
                }

                clip: true

                StyledRect {
                    anchors.top: parent.top
                    anchors.left: parent.left
                    anchors.right: parent.right
                    implicitHeight: parent.implicitHeight * 2
                    color: Colours.palette.m3primary
                    radius: Tokens.rounding.full
                }

                Behavior on x {
                    Anim {}
                }
                Behavior on implicitWidth {
                    Anim {}
                }
            }
        }
    }

    Loader {
        id: windowSwitcherList

        asynchronous: true
        active: root.state === "windowSwitcher"

        anchors.top: parent.top
        anchors.bottom: parent.bottom
        anchors.horizontalCenter: parent.horizontalCenter

        sourceComponent: WindowSwitcherList {
            search: root.search
            visibilities: root.visibilities
            panels: root.panels
            content: root.content
        }
    }

    Loader {
        id: keybindsList

        active: root.state === "keybinds"

        anchors.fill: parent

        sourceComponent: KeybindsList {
            search: root.search
            visibilities: root.visibilities
        }
    }

    Loader {
        id: animationsList

        active: root.state === "animations"

        anchors.fill: parent

        sourceComponent: AnimationsList {
            search: root.search
            visibilities: root.visibilities
        }
    }

    Row {
        id: empty

        opacity: root.currentList?.count === 0 ? 1 : 0
        scale: root.currentList?.count === 0 ? 1 : 0.5

        spacing: Tokens.spacing.medium
        padding: Tokens.padding.large

        anchors.horizontalCenter: parent.horizontalCenter
        anchors.verticalCenter: parent.verticalCenter

        MaterialIcon {
            text: {
                if (root.state === "wallpapers")
                    return "wallpaper_slideshow";
                if (root.state === "keybinds")
                    return "keyboard";
                if (root.state === "animations")
                    return "animation";
                return "manage_search";
            }
            color: Colours.palette.m3onSurfaceVariant
            fontStyle: Tokens.font.icon.extraLarge

            anchors.verticalCenter: parent.verticalCenter
        }

        Column {
            anchors.verticalCenter: parent.verticalCenter

            StyledText {
                text: {
                    if (root.state === "wallpapers")
                        return "No se encontraron fondos";
                    if (root.state === "keybinds")
                        return "No se encontraron atajos";
                    if (root.state === "animations")
                        return "No se encontraron animaciones";
                    return "Sin resultados";
                }
                color: Colours.palette.m3onSurfaceVariant
                font: Tokens.font.body.builders.large.weight(Font.Medium).build()
            }

            StyledText {
                text: {
                    if (root.state === "wallpapers")
                        return Wallpapers.list.length === 0 ? "Prueba a poner fondos en %1".arg(Paths.shortenHome(Paths.wallsdir)) : "Prueba a buscar otra cosa";
                    if (root.state === "keybinds")
                        return "Ningún atajo coincide con tu búsqueda";
                    if (root.state === "animations")
                        return "Prueba añadir archivos .lua a\n~/.config/caelestia/animaciones/";
                    return "Prueba a buscar otra cosa";
                }
                color: Colours.palette.m3onSurfaceVariant
                font: Tokens.font.body.medium
            }
        }

        Behavior on opacity {
            Anim {
                type: Anim.DefaultEffects
            }
        }

        Behavior on scale {
            Anim {}
        }
    }

    Behavior on implicitWidth {
        enabled: root.visibilities.launcher

        Anim {}
    }

    Behavior on implicitHeight {
        enabled: root.visibilities.launcher

        Anim {}
    }
}
